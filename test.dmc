#AUTO
#configuremotors,encoder,andcoordsys
MT-2.5,-2,1,1
CE 0,2
DP0,0
DE0,0
curx=0
cury=0
SH
JG0,0
BGXY
DC2000000,2000000
AC2000000,2000000
oncam=1
SB1
CB 2
CB 3
deltax=908779
deltay=9610
#LOOP
JG
xangle=(@AN[1]-2.69)/2.38
yangle=(@AN[2]-2.29)/2.42
JP#XNOTLOW,.07<@ABS[xangle]
xangle=0
#XNOTLOW
JP#XNOTHI,0.9>@ABS[xangle]
xangle=0.9*xangle/@ABS[xangle]
#XNOTHI
JGX=((xangle*xangle*xangle*550300)-(xangle*1326))*(1-@IN[8])
JP#YNOTLOW,.07<@ABS[yangle]
yangle=0
#YNOTLOW
JP#YNOTHI,0.9>@ABS[yangle]
yangle=0.9*yangle/@ABS[yangle]
#YNOTHI
JGY=((yangle*yangle*yangle*550300)-(yangle*1326))*(1-@IN[7])
JP#LOOP,((@ABS[xangle]>0.1)|(@ABS[yangle]>0.1))
JP#SETORIG,@IN[6]=0
JP#HOME,@IN[17]=0
#PADLOOP
a3=@AN[3]
IF(a3<0)
a3=0
ENDIF
IF(a3>5)
a3=5
ENDIF
IF(a3<0.68)
jogspd=10
ENDIF
IF((a3>=0.68)&(a3<2.54))
jogspd=(48.39*a3)-22.9
ENDIF
IF(a3>=2.54)
jogspd=(26450*a3*a3*a3)-(201500*a3*a3)+(514400*a3)-439916
ENDIF
IF(@IN[2]=0)
JG0,jogspd;JP#PADLOOP
ENDIF
IF(@IN[3]=0)
JG0,-jogspd;JP#PADLOOP
ENDIF
IF(@IN[4]=0)
JG-jogspd,0;JP#PADLOOP
ENDIF
IF(@IN[5]=0)
JGjogspd,0;JP#PADLOOP
ENDIF
JP#LOOP,@IN[1]=1
#SWITCHORIGIN
ST
MC
LMXY
VS400000;VA2000000;VD 2000000
IF(oncam=1)
curx=_TPA
cury=_TPB
NO Move offset amount (considering your current location)
NO and overshooting 1mm in both directions on purpose.
LIdeltax-curx-1000,deltay-cury-1000
LE;BGS
MC
WT100
NO Next we measure where we are, using the encoders, and
NO calculate the required movement to complete the shift.
actx=_TPA
acty=_TPB
VS40000;VA400000;VD 400000
ST;MC;LMXY
LI(deltax-actx),(deltay-acty)
LE;BGS;MC
WT100
NO We should be right under the laser, but we will make one
NO final correction move to make absolutely sure.
actx=_TPA
acty=_TPB
ST;MC;LMXY
LI(deltax-actx),(deltay-acty)
LE;BGS
MC
NO This flag says we are not under the laser.
oncam=0
NO Darken the "CAM VIEW" light.
CB 1
ELSE
NO The sequence below is similar, but moves us from the
NO laser view to the camera view.  Same 3-move sequence.
IF(oncam=0)
curx=_TPA
cury=_TPB
LI-curx-1000,-cury-1000
LE;BGS
MC
WT100
actx=_TPA
acty=_TPB
VS40000;VA400000;VD 400000
ST;MC;LMXY
LI-actx,-acty
LE;BGS
MC
WT100
actx=_TPA
acty=_TPB
ST;MC;LMXY
LI-actx,-acty
LE;BGS
MC
oncam=1
SB1
ENDIF
ENDIF
JG0,0
BGX,Y
JP#LOOP
#HOME
ST
MC
LMXY
VS400000;VA2000000;VD 2000000
IF(oncam=1)
curx=_TPA
cury=_TPB
LI-curx-1000,-cury-1000
LE;BGS
MC
WT100
actx=_TPA
acty=_TPB
VS40000;VA400000;VD 400000
ST;MC;LMXY
LI-actx,-acty
LE;BGS
MC
WT100
actx=_TPA
acty=_TPB
ST;MC;LMXY
LI-actx,-acty
LE;BGS
MC
ELSE
curx=_TPA
cury=_TPB
LI-curx-1000+deltax,-cury-1000+deltay
LE;BGS
MC
WT100
actx=_TPA
acty=_TPB
VS40000;VA400000;VD 400000
ST;MC;LMXY
LI-actx+deltax,-acty+deltay
LE;BGS
MC
WT100
actx=_TPA
acty=_TPB
ST;MC;LMXY
LI-actx+deltax,-acty+deltay
LE;BGS
MC
ENDIF
JG0,0
BGX,Y
JP#LOOP
#SETORIG
ST
MC
DP0,0
DE0,0
IF(oncam=1)
DP0,0
DE0,0
ELSE
DPdeltax,deltay
DEdeltax,deltay
ENDIF
bit2=@OUT[2]
bit3=@OUT[3]
CB 2;CB 3
loopnum=0
#WAIT
JP#WAIT,@IN[6]=0
JG0,0
BGX,Y
JP#LOOP
EN
#LIMSWI
DC10000000,10000000
ST
MC
DC2000000,2000000
SP200000,200000
IF(_LFX=0)
NOIMG "+X limit hit!"
SB2
PR-50000,0;BGX;MC
ENDIF
IF(_LRX=0)
NOMG "-X limit hit!"
SB2
PR50000,0;BGX;MC
ENDIF
IF(_LFY=0)
NOMG "+Y limit hit!"
SB3
PR0,-50000;BGY;MC
ENDIF
IF(_LRY=0)
NOMG "-Y limit hit!"
SB3
PR0,50000;BGY;MC
ENDIF
WT500
CB 2
CB 3
JG0,0
BGXY
RE