#AUTO

MT -2,-2,1,1  	;' Configure the motors
CE 0,0          	;' Configure encoders to quadrature and reverse quad
DP 0,0          	;' Define the position of the encoders
DE 0,0          	;' same for aux encoders

curx=0          	;' vars for position
cury=0			

SH			
JG 0,0 			;' Set jog moder and slew speeds
BG XY 			;' Begin moving on these access (doesnt do anything I think)

DC 2000000,2000000 	;' Set deceleration and acceleration values
AC 2000000,2000000

oncam=1 		;' Set cam as starting coord sys
deltax=	1099635;' This is the x displacement between cam and cut origins
deltay=-63566




jogspd = 5000

d_up = 0
d_down = 0
d_left = 0
d_right = 0

ndg_up = 0
ndg_dwn = 0
ndg_lft = 0
ndg_rt = 0
ndg_dst = 1000

b_switch = 0
b_home = 0
b_set = 0

x_enable = 0
y_enable = 0

homex = _TPA
homey = _TPB

a3 = 0 ;' speed select for nudges

#LOOP
JG 	;' Make sure we are in jog mode.

JP#SWITCH,b_switch=1 ;' Switch origins if triggered
JP#HOME,b_home=1 ;' Home motor to relative origin 
JP#SETORIG,b_set=1 ;' Switch origins if triggered

;' Read in analog voltage
xangle=(@AN[1]-2.583)/2.583
yangle=(@AN[2]-2.583)/2.583

JP#XNOTLOW,.07<@ABS[xangle] 	;' If x or y < 0.07, make 0
xangle=0 
#XNOTLOW

JP#XNOTHI,0.9>@ABS[xangle]
xangle=0.9*xangle/@ABS[xangle]

#XNOTHI
JGX=((xangle*xangle*xangle*550300)-(xangle*1326))*(x_enable) 	
;' Otherwise map to jog speed

JP#YNOTLOW,.07<@ABS[yangle]
yangle=0

#YNOTLOW
JP#YNOTHI,0.9>@ABS[yangle]
yangle=0.9*yangle/@ABS[yangle]

#YNOTHI
JGY=((yangle*yangle*yangle*550300)-(yangle*1326))*(y_enable)

JP#LOOP,((@ABS[xangle]>0.1)|(@ABS[yangle]>0.1)) 		
;' If still joysticking keep looping

#PADLOOP 							
;' Loop for keypad nudge movements

IF (d_up=1)
  JGY=jogspd;JP#PADLOOP
ENDIF
IF (d_down=1)
  JGY=-jogspd;JP#PADLOOP
ENDIF
IF (d_left=1)
  JGX=-jogspd;JP#PADLOOP
ENDIF
IF (d_right=1)
  JGX=jogspd;JP#PADLOOP
ENDIF

IF (ndg_up=1)
  ndg_up=0
  ST
  MC
  LM XY
  VS jogspd;VA 2000000;VD 2000000;
  LI 0, -ndg_dst
  LE;BGS
  MC
  JG 0,0
  BG X,Y

ENDIF

IF (ndg_dwn=1)
  ndg_dwn=0
  ST
  MC
  LM XY
  VS jogspd;VA 2000000;VD 2000000;
  LI 0, ndg_dst
  LE;BGS
  MC
  JG 0,0
  BG X,Y
ENDIF

IF (ndg_lft=1)
  ndg_lft=0  
  ST
  MC
  LM XY
  VS jogspd;VA 2000000;VD 2000000;
  LI -ndg_dst, 0
  LE;BGS
  MC
  JG 0,0
  BG X,Y
ENDIF

IF (ndg_rt=1)
  ndg_rt=0
  ST
  MC
  LM XY
  VS jogspd;VA 2000000;VD 2000000;
  LI ndg_dst,0
  LE;BGS
  MC
  JG 0,0
  BG X,Y
ENDIF

JP#LOOP

#SWITCH 
ST
MC
LM XY
VS 400000;VA 2000000;VD 2000000;
IF (oncam=1) 
  movex=_TPA - deltax
  movey=_TPB - deltay
  LI movex-1000,movey-1000
  LE;BGS 
  MC     
  WT 100 
  actx=_TPA
  acty=_TPB
  VS 40000;VA 400000;VD 400000;
  ST;MC;LM XY
  LI actx+movex,acty+movey
  LE;BGS;MC
  WT 500
  VS 4000;VA 40000;VD 40000;
  actx=_TPA
  acty=_TPB
  ST;MC;LM XY
  LI actx+movex,acty+movey
  LE;BGS
  MC
  oncam=0
  CB 1
  DP 0,0
  DE 0,0
ELSE
  IF (oncam=0)
    movex=_TPA + deltax
    movey=_TPB + deltay
    LI movex-1000,movey-1000
    LE;BGS
    MC
    WT 100
    actx=_TPA
    acty=_TPB
    VS 40000;VA 400000;VD 400000;
    ST;MC;LM XY
    LI actx+movex,acty+movey
    LE;BGS
    MC
    WT 500
    VS 4000;VA 40000;VD 40000;
    actx=_TPA
    acty=_TPB
    ST;MC;LM XY
    LI actx+movex,acty+movey
    LE;BGS
    MC
    oncam=1
    SB 1
    DP 0,0
    DE 0,0
  ENDIF
ENDIF
DP 0,0
DE 0,0
b_switch = 0
homex = _TPA
homey = _TPB
JG 0,0
BG X,Y
JP#LOOP


#HOME
ST
MC
LM XY
VS 400000;VA 2000000;VD 2000000;
  curx=_TPA
  cury=_TPB
  LI -1000,-1000
  LE;BGS
  MC
  WT 100
  actx=_TPA
  acty=_TPB
  VS 40000;VA 400000;VD 400000;
  ST
  MC
  LM XY
  LI actx,acty
  LE;BGS
  MC
  WT 100
  actx=_TPA
  acty=_TPB
  VS 4000;VA 40000;VD 40000;
  ST
  MC
  LM XY
  LI actx,acty
  LE;BGS
  MC
b_home = 0
homex = _TPA
homey = _TPB
JG 0,0
BG X,Y
JP#LOOP


#SETORIG
ST
MC
DP 0,0
DE 0,0
IF (oncam=1)
  DP 0,0
  DE 0,0
  homex = 0
  homey = 0
ELSE
  DP 0, 0
  DE 0, 0
  homex = 0
  homey = 0
ENDIF
b_set = 0
JG 0,0
BG X,Y
JP#LOOP

#HOMEMOT
SP 50000, 50000
HM 
BG XY
AM XY
ST
MC
LM XY
VS 40000;VA 20000;VD 20000;
LI 1560000,850000
LE;BGS
MC
oncam = 1
JP#SETORIG

#LIMSWI
DC 10000000,10000000
ST
MC
DC 2000000,2000000
SP 200000,200000
IF (_LFX=0)
  NOIMG "+X limit hit!"
  SB 2
  PR -50000,0;BGX;MC
ENDIF
IF (_LRX=0)
  NOMG "-X limit hit!"
  SB 2
  PR 50000,0;BGX;MC
ENDIF
IF (_LFY=0)
  NOMG "+Y limit hit!"
  SB 3
  PR 0,-50000;BGY;MC
ENDIF
IF (_LRY=0)
  NOMG "-Y limit hit!"
  SB 3
  PR 0,50000;BGY;MC
ENDIF
WT 500
CB 2
CB 3
JG 0,0
BG XY
RE


